dnl Process this file with autoconf to produce a configure script.
AC_INIT(pigale,1.2.8,[hf@sourceforge.net],Public Implementation of a Graph Library and Editor)
dnl CURRENT:REVISION:AGE
LIB_VERSION=1:26:0
AC_SUBST(LIB_VERSION)
AC_PREFIX_DEFAULT(/usr/local/pigale-$PACKAGE_VERSION) 
if test "$prefix" != NONE; then
	ppath="$prefix"
else
	ppath="/usr/local/pigale-$PACKAGE_VERSION"
fi
AC_DEFINE_UNQUOTED(PACKAGE_PATH,"$ppath",Path to Pigale root directory)

dnl Checks target processor
AC_CANONICAL_SYSTEM
CPU=
case "$target_cpu" in
      i686) CPU="-mcpu=i686";;
      i586) CPU="-mcpu=i586";;
      i486) CPU="-mcpu=i486";;
      i386) CPU="-mcpu=i386";;
esac


AM_INIT_AUTOMAKE(pigale,$PACKAGE_VERSION)
AC_CONFIG_HEADER(incl/config.h:config.h.in)
AH_TOP([
#ifndef _CONFIG_H
#define _CONFIG_H
])
AH_BOTTOM([#endif])

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

MYDEFS=" -DVERSION_BETA -DQT"

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CXX
AC_LANG_CPLUSPLUS
AC_PROG_LN_S
AC_PROG_MAKE_SET


AC_MSG_CHECKING([for QT])

AC_ARG_WITH(qt-includes,
    [  --with-qt-includes=DIR   where the QT includes are. ],
    [  QT_PATH="$withval"],[
QT_PATH=$QTDIR/include:/usr/include/qt:/usr/lib/qt/include:/usr/local/qt/include:/usr/include:/usr/local/lib/qt/include
])

AC_PATH_PROG(QINC,[qgl.h],,[$QT_PATH])
if test "x$QINC" = x; then
  AC_MSG_ERROR(No QT include found)
else
  QT_INCLUDES=-I$AS_DIRNAME([$QINC])
fi

AC_ARG_WITH(qt-libraries,
    [  --with-qt-libraries=DIR where the QT libraries are. ],
    [  QTPATH="$withval"],[
QTPATH=$QTDIR/lib:/usr/lib/qt/lib:/usr/local/lib/qt/lib:/usr/lib/:/usr/local/lib/
    ])

AC_PATH_PROG(QLIB,[libqui.so],,[$QTPATH])
if test "x$QLIB" = x; then
  AC_MSG_ERROR(No QT librairie found)
else
  QT_LDFLAGS=-L$AS_DIRNAME([$QLIB])
fi
LIB_QT="-lqui"

AC_ARG_WITH(qt-binaries,
    [  --with-qt-binaries=DIR where the QT binaries are. ],
    [  QTPATH="$withval"],[
QTPATH=$PATH:$ac_qt_bindir:$QTDIR/bin:/usr/bin:/usr/X11R6/bin:/usr/lib/qt/bin:/usr/local/qt/bin
    ])

AC_PATH_PROG(MOC,moc,,[$QTPATH])
if test "x$MOC" = x; then
  AC_MSG_ERROR(No moc found)
fi
AC_PATH_PROG(LRELEASE,lrelease,,[$QTPATH])
if test "x$LRELEASE" = x; then
  AC_MSG_WARN(No lrelease found)
fi
AC_SUBST(QT_INCLUDES)
AC_SUBST(MOC)
AC_SUBST(LRELEASE)


AC_ARG_ENABLE(debug,
[  --enable-debug          enable full pigale debugging [default=off]],
  enable_debug=$enableval, enable_debug=no)
if test "x$enable_debug" = xyes; then
	MYDEFS="$MYDEFS -DTDEBUG"
  	if test "x$GXX" = xyes; then
    		CXXFLAGS="$CXXFLAGS -O0 "
  	fi
elif test  "$GXX" = "yes";then
	CXXFLAGS="-O3 $CPU -pipe -Wall -Wstrict-prototypes -Wpointer-arith -Werror -fomit-frame-pointer"
fi

dnl Checks for X11, OpenGL, Qt,moc
AC_PATH_XTRA
dnl AC_PATH_QT_MOC
dnl AC_PATH_QT_LRELEASE
dnl AC_PATH_QT
LDFLAGS="${LDFLAGS} ${X_LIBS} ${X_EXTRA_LIBS}"
dnl AC_PATH_GL

dnl Checks for headers
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(GL/gl.h)
AC_CHECK_HEADERS(GL/glut.h,,AC_MSG_WARN([
Glut header not found.
]))

dnl Checks for libraries.
AC_CHECK_LIB([Xi], [XOpenDevice])
AC_CHECK_LIB([Xmu], [XmuLookupStandardColormap])
AC_CHECK_LIB(GL, glEnd)
AC_CHECK_LIB(GLU, gluSphere)
AC_CHECK_LIB(glut, glutStrokeCharacter)


LDFLAGS="$LDFLAGS $QT_LDFLAGS $LIB_QT"
GCPPFLAGS="$MYDEFS $CPPFLAGS"
CPPFLAGS="$GCXXFLAGS -DQT_THREAD_SUPPORT $QT_INCLUDES"

dnl Checks for Qt library
AC_MSG_CHECKING(for Qt modules)
AC_TRY_COMPILE([
#include <qmodules.h>
#ifndef QT_MODULE_KERNEL
#error
#endif
#ifndef QT_MODULE_WIDGETS
#error
#endif
#ifndef QT_MODULE_DIALOGS
#error
#endif
#ifndef QT_MODULE_ICONVIEW
#error
#endif
#ifndef QT_MODULE_WORKSPACE
#error
#endif
#ifndef QT_MODULE_NETWORK
#error
#endif
#ifndef QT_MODULE_CANVAS
#error
#endif
#ifndef QT_MODULE_XML
#error
#endif
#ifndef QT_MODULE_OPENGL
#error
#endif
],,AC_MSG_RESULT(yes),AC_MSG_ERROR([
Missing some necessary Qt modules]))

AC_MSG_CHECKING(for threads in Qt library)
AC_TRY_LINK([
#include <qthread.h>
class Toto: public QThread { void run() {} };
],[
Toto x;
],[
have_qt_thread=yes
AC_MSG_RESULT(yes)
],AC_MSG_ERROR([
Unable to find threads in Qt library. If you
just installed it: try running ldconfig
as root]))
if test "x$have_qt_thread"=xyes; then
  AC_DEFINE(QT_THREAD_SUPPORT,1,Qt Thread Support)
fi

CPPFLAGS="$GCPPFLAGS"

echo "############### RESULTS: ######################"
echo prefix:$prefix
echo libs:$LIBS
echo headers:$QT_INCLUDES $X_CFLAGS
echo compile:$CXXFLAGS
echo moc:$MOC
echo lrelease:$LRELEASE
echo cpp:$CPPFLAGS

AC_PROG_INSTALL
AC_OUTPUT(
Makefile
tgraph/Makefile
qt/Makefile
)


